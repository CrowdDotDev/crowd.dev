profile = "chat/members@1.0"
provider = "slack"

"""
GetMembers map
"""
map GetMembers {
    http GET "/users.list" {
		request "application/x-www-form-urlencoded" {
			query {
				cursor = input.page
				limit = input.limit || 100
			}

			headers {
				"Authorization" = `Bearer ${parameters.accessToken}`
			}
		}

		response 200 "application/json" {
			error = call MapSlackError(
				statusCode = statusCode,
				error = body,
				headers = headers,
			) if (!body.ok)

			return map error if (!body.ok) error

			members = call MapMembers(members = body.members)
			rateLimit = call MapRateLimitDetails(headers = headers)
			result = { members: members }

			set if (body.response_metadata && body.response_metadata.next_cursor) {
				result.nextPage = body.response_metadata.next_cursor
			}

			set if (rateLimit) {
				result.rateLimit = rateLimit
			}

			map result result
		}

		response {
			error = call MapSlackError(
				statusCode = statusCode,
				error = body,
				headers = headers,
			)
			
			map error error
		}
	}
}

"""
Operation MapMembers
Returns array mapping members in received body
"""
operation MapMembers {
	members = []
	
	members = call foreach(index of Array(args.members.length).fill().map((_, i) => i)) MapMember(
		index = index,
		members = args.members,
	) if (args.members && args.members.length > 0)

	return members
}

operation MapMember {
	index = args.index
	member = args.members[index]

    resultMember = {
        id: member.id,
        isBot: member.is_bot
    }

    set if (member.profile && member.profile.display_name) {
        resultMember.username = member.profile.display_name
    }

    set if (!resultMember.username) {
        resultMember.username = member.name
    }

	return resultMember
}

"""
Operation MapRateLimitDetails
Returns object mapping rate limit details specified in received headers
"""
operation MapRateLimitDetails {
	headers = args.headers
	details = {}

	totalRequests = parseInt(headers['X-Rate-Limit-Limit'])
	remainingRequests = parseInt(headers['X-Rate-Limit-Remaining'])
	resetAfter = parseInt(headers['X-Rate-Limit-Reset'])
	retryAfter = parseInt(headers['Retry-After'])

	set if (!isNaN(totalRequests)) {
		details.totalRequests = totalRequests
	}

	set if (!isNaN(remainingRequests)) {
		details.remainingRequests = remainingRequests
	}

	set if (!isNaN(resetAfter)) {
		details.resetAfter = resetAfter
		details.resetTimestamp = Date.now() + (resetAfter * 1000)
	}

	set if (!isNaN(retryAfter)) {
		details.retryAfter = retryAfter
	}

	set if (details.remainingRequests && details.totalRequests) {
		details.remainingRequestsPercentage = (details.remainingRequests / details.totalRequests) * 100
	}

	set if (Object.keys(details).length === 0) {
		details = undefined
	}

	return details
}

"""
Operation Map Slack Error
Returns error object based on error body, statusCode and headers.
See https://api.slack.com/methods/conversations.history#errors for more information about errors
"""
operation MapSlackError {
	statusCode = args.statusCode
	slackError = args.error
	headers = args.headers

	rateLimit = call MapRateLimitDetails(headers = headers)
	detail = slackError.error

	return if (statusCode === 200) {
		title = detail
		rateLimit = rateLimit
	}

	return if (statusCode === 400) {
		title = 'Bad request'
		detail = slackError
		rateLimit = rateLimit
	}

	return if (statusCode === 401) {
		title = 'Unauthorized'
		detail = detail
		rateLimit = rateLimit
	}

	return if (statusCode === 403) {
		title = 'Forbidden'
		detail = detail
		rateLimit = rateLimit
	}

	return if (statusCode === 404) {
		title = 'Not found'
		detail = detail
		rateLimit = rateLimit
	}

	return if (statusCode === 405) {
		title = 'Method not allowed'
		detail = detail
		rateLimit = rateLimit
	}

	return if (statusCode === 429) {
		title = 'Too Many Requests'
		detail = `Retry after ${headers['Retry-After']} seconds`
		rateLimit = rateLimit
	}

	return if (statusCode === 502) {
		title = 'Bad gateway'
		detail = detail
		rateLimit = rateLimit
	}

	return {
		title = 'Unknown error'
		detail = `Unknown error occurred. Status: ${statusCode}. Code: ${detail}.`
		rateLimit = rateLimit
	}
}

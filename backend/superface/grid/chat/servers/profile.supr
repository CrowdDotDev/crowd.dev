name = "chat/servers"
version = "1.0.0"

"""
GetServers usecase
"""
usecase GetServers {
    input {
        """
        As Bot
        Boolean representing whether to authorize as bot
        """
        asBot boolean
	}

    result {
        """
        Servers
        List of servers, can be workspaces or guilds
        """
        servers [Server]

        """
        Rate Limit
        Rate limit details.
        """
        rateLimit RateLimit
    }

	error Error
}

model Server {
    id! string!
	createdAt! number!
    name string
    icon string
    description string
}   

model Error {
    """
	Title
	A short, human-readable summary of the problem type.
	"""
	title!

	"""
	A human-readable explanation specific to this occurrence of the problem.
	"""
    detail

	"""
	Rate Limit
	Rate limit details.
	"""
	rateLimit RateLimit
}

model RateLimit {
  """
  Bucket
  Different parts of API may have different rate limits.
  Bucket identifies to which part of API the rate limits apply.
  """
  bucket string

  """
  Total Requests
  Total requests available in the time window.
  """
  totalRequests number

  """
  Remaining Requests
  Remaining requests available in the time window.
  """
  remainingRequests number

  """
  Remaining Requests available in percents
  Remaining requests available in the time window in percents.
  """
  remainingRequestsPercentage number

  """
  Reset Timestamp
  Timestamp when the rate limits will reset (in  Unix Timestamp format).
  """
  resetTimestamp number

  """
  Retry After
  Number of seconds until reset of rate limit
  """
  retryAfter number
}

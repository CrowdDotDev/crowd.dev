name = "oauth2/refresh-token"
version = "1.0.0"

"""
Get Access Token from Refresh Token
Issue a new access token based on previously issued refresh token for OAuth 2.0 compatible providers.
Client authentication can be provided directly as input, or, if supported, set as provider parameter.
"""
usecase GetAccessTokenFromRefreshToken unsafe {
  input {
    """
    Refresh Token
    The refresh token previously issued to the client.
    """
    refreshToken! string!

    """
    Client ID
    Client authentication; may be also accepted as provider parameter.
    """
    clientId string!

    """
    Client Secret
    Client authentication; may be also accepted as provider parameter.
    """
    clientSecret string!
  }

  result {
    """
    Access Token
    Newly issued access token
    """
    accessToken! string!
    """
    Expires in (seconds)
    Duration of time the access token is granted for.
    """
    expiresIn number!
    """
    Scopes
    Authorized scopes for the issued access token.
    """
    scopes [string]
    """
    Token type
    The type of token this is, usually "Bearer".
    """
    tokenType string

    """
    Refresh token
    Provider may issue a new refresh token, invalidating the previously used refresh token
    """
    refreshToken string
  }

  error {
    "Error code"
    error enum {
      invalid_request
      invalid_client
      invalid_grant
      invalid_scope
      unauthorized_client
      unsupported_grant_type
      // If we cannot map the error code:
      unknown_response
    }
    "Error description"
    description
    "Link with error detail"
    link
  }
}
"""
Publish a Post
Publish a Post on social media such as Instagram, Facebook or Twitter.
"""

name = "social-media/publish-post"
version = "1.0.0"

"""
Publish a Post
Publish post on social media profile.
"""
usecase PublishPost unsafe {
  input {
    """
    Profile ID
    Identification of profile to publish to; may be optional with some providers
    """
    profileId string!

    """
    Text
    Text of the post
    """
    text string!

    """
    Link
    URL to attach to the post
    """
    link string!

    """
    Media attachments
    Only images and gifs are supported
    """
    media [
      MediaItem
    ]
  }

  result {

    """
    Post ID
    Identifier of the post that was published
    """
    postId string

    """
    Post URL
    Link to the published post
    """
    url string
  }

  error {
    """
    Title
    A short, human-readable summary of the problem type.
    """
    title!

    """
    A human-readable explanation specific to this occurrence of the problem.
    """
    detail
  }

  example Successful {
    input {
      profileId = '110658944785122',
      text = 'Text of the Post.',
    }

    result {
      postId = '110658944785122_125682506616099',
      url = 'https://www.facebook.com/110658944785122/posts/125682506616099/',
    }
  }

  example Failed {
    input {
      profileId = 'invalid profile ID',
      text = 'Text of the Post.',
    }

    error {
      title = 'Bad request'
    }
  }
}

model MediaItem {
  """
  URL of image
  Will be fetched by provider, must be accessible publicly. 
  If both `contents` and `url` fields are provided, a preferable option will be picked by the map.
  """
  url string!
  """
  Buffer or base64 encoded string with image
  If both `contents` and `url` fields are provided, a preferable option will be picked by the map.
  """
  contents
  "Alternative text for screen readers"
  altText string
}